# Maintainer: frap129 <joe@maples.dev>

pkgname=hyperfox-browser-bin
provides=(${pkgname//-bin/""})
conflicts=(${pkgname//-bin/""})
pkgver=VERSION
pkgrel=RELEASE
pkgdesc="An optimized distribution of LibreWolf - PGO, LTO, and more"
arch=(x86_64)
license=(MPL GPL LGPL)
url="https://github.com/frap129/hyperfox"
depends=(gtk3 libxt startup-notification mime-types dbus
  nss ttf-font libpulse ffmpeg4.4 zlib)
makedepends=(git)
optdepends=(
  'hunspell-en_US: Spell checking, American English'
  'libnotify: Notification integration'
  'networkmanager: Location detection via available WiFi networks'
  'pulseaudio: Audio support'
  'speech-dispatcher: Text-to-Speech'
  'xdg-desktop-portal: Screensharing with Wayland'
)
backup=('usr/lib/hyperfox/librewolf.cfg'
  'usr/lib/hyperfox/distribution/policies.json')
options=(!emptydirs)
install='hyperfox-bin.install'

_project_id=44042130
_source_tag="v${pkgver}-${pkgrel}"
_base_url=https://github.com/frap129/${pkgname//-browser-bin/""}/releases/download/${_source_tag}
_uploadpath_x86_64=${_base_url}/${pkgname//-browser-bin/""}-${pkgver}-${pkgrel}.en-US.linux-x86_64.tar.xz
source=(
  "git+https://github.com/frap129/${pkgname//-browser-bin/""}.git"
  default192x192.png
  hyperfox.desktop
)
source_x86_64=("${_uploadpath_x86_64}")
sha256sums=('SKIP'
  '959c94c68cab8d5a8cff185ddf4dca92e84c18dccc6dc7c8fe11c78549cdc2f1'
  '2b560ff8d0c3efb328599be2cee6ab75ad47ad6609e2db45007463fcefbc4f82')
sha256sums_x86_64=('SHA256SUM')

package() {
  # Yep, that's somewhat redundant. But it works.
  install -dm 755 ${pkgdir}/usr/lib/hyperfox
  install -dm 755 ${pkgdir}/usr/bin
  cp -r "${srcdir}"/${pkgname//-browser-bin/""}/* "${pkgdir}"/usr/lib/${pkgname//-browser-bin/""}

  cd ${srcdir}/${pkgname//-browser-bin/""}

  local vendorjs="$pkgdir/usr/lib/${pkgname//-browser-bin/""}/browser/defaults/preferences/vendor.js"

  install -Dvm644 /dev/stdin "$vendorjs" <<END
// Use system-provided dictionaries
pref("spellchecker.dictionary_path", "/usr/share/hunspell");

// Don't disable extensions in the application directory
// done in librewolf.cfg
// pref("extensions.autoDisableScopes", 11);
END

  local distini="$pkgdir/usr/lib/${pkgname//-browser-bin/""}/distribution/distribution.ini"
  install -Dvm644 /dev/stdin "$distini" <<END

[Global]
id=io.gitlab.${pkgname//-browser-bin/""}-community
version=1.0
about=Hyperfox

[Preferences]
app.distributor="frap129"
app.distributor.channel=${pkgname//-browser-bin/""}
app.partner.librewolf=${pkgname//-browser-bin/""}
END

  for i in 16 32 48 64 128; do
    install -Dvm644 ${srcdir}/${pkgname//-browser-bin/""}/themes/browser/branding/librewolf/default$i.png \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/${pkgname//-browser-bin/""}.png"
  done
  # install -Dvm644 browser/branding/librewolf/content/about-logo.png \
  # "$pkgdir/usr/share/icons/hicolor/192x192/apps/$pkgname.png"
  install -Dvm644 ${srcdir}/default192x192.png \
    "$pkgdir/usr/share/icons/hicolor/192x192/apps/${pkgname//-browser-bin/""}.png"

  # arch upstream provides a separate svg for this. we don't have that, so let's re-use 16.png
  install -Dvm644 ${srcdir}/${pkgname//-browser-bin/""}/themes/browser/branding/librewolf/default16.png \
    "$pkgdir/usr/share/icons/hicolor/symbolic/apps/${pkgname//-browser-bin/""}-symbolic.png"

  install -Dvm644 ${srcdir}/${pkgname//-browser-bin/""}.desktop \
    "$pkgdir/usr/share/applications/${pkgname//-browser-bin/""}.desktop"

  # Install a wrapper to avoid confusion about binary path
  install -Dvm755 /dev/stdin "$pkgdir/usr/bin/${pkgname//-browser-bin/""}" <<END
#!/bin/sh
exec /usr/lib/${pkgname//-browser-bin/""}/${pkgname//-browser-bin/""} "\$@"
END

  # Replace duplicate binary with wrapper
  # https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -srfv "$pkgdir/usr/bin/${pkgname//-browser-bin/""}" "$pkgdir/usr/lib/${pkgname//-browser-bin/""}/${pkgname//-browser-bin/""}-bin"
  # Use system certificates
  local nssckbi="$pkgdir/usr/lib/${pkgname//-browser-bin/""}/libnssckbi.so"
  if [[ -e $nssckbi ]]; then
    ln -srfv "$pkgdir/usr/lib/libnssckbi.so" "$nssckbi"
  fi

}
